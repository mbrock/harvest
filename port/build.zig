//! Builds the modern port of Harvest from the recovered source in ../src and the port's own
//! sources in src/.
//!
//! The kept units are every unit in config/1.18-linux-amd64/units.toml except the platform units the
//! port replaces (`replaced` below), so newly recovered units join the build without edits here.
//! Port sources are every .cpp under src/ except src/main.cpp, so new platform code joins the same
//! way. Dependencies are pinned in build.zig.zon and built from source for the target.
//!
//! For the web (-Dtarget=wasm32-emscripten) zig compiles everything against Emscripten's sysroot and
//! Emscripten only links: the `harvest` step installs the page from web/, harvest.js and
//! harvest.wasm. em-config and em++ come from PATH.

const std = @import("std");

const units_toml = "../config/1.18-linux-amd64/units.toml";

/// Recovered units the port replaces with its own implementations, as path prefixes under src/.
const replaced = [_][]const u8{
    // Linux device (X11/SFML window and events) and OS operator (GTK clipboard): the SDL3 device
    // replaces them. The device stub, logger and os helpers are kept: the SDL3 device derives from
    // CIrrDeviceStub as the Linux device did.
    "daisy/other/CIrrDeviceLinux.cpp",
    "daisy/other/CLinuxOperator.cpp",
    // SFML joysticks: SDL3 gamepads replace them. The null driver is kept.
    "daisy/input/CJoystickLinuxDriver.cpp",
    // OpenAL/ALUT backend: miniaudio replaces it.
    "daisy/audio/COpenALDriver.cpp",
    // OpenGL 1.x driver, its textures and Cg/GLSL/ARB material renderers: the GLES3 renderer
    // replaces them.
    "daisy/video/OpenGL/",
    "daisy/video/Null/CCGMaterialRenderer.cpp",
    // The software renderer's z-buffer, never reached by the game.
    "daisy/video/Software/",
    // The blocking main loop: the port runs the frame step from SDL3's main callbacks.
    "HarvestFull/main.cpp",
};

/// Flags for the recovered C++ and the port's C++. GCC 4.4 defaulted to gnu++98, which needs no
/// source changes.
const cxx_flags = [_][]const u8{
    "-std=gnu++98",
    "-DHARVEST_PORT",
    // sprintf and friends in the original source, and string literals bound to char*.
    "-Wno-deprecated-declarations",
    "-Wno-c++11-compat-deprecated-writable-strings",
    // The original relies on two's-complement wrapping in its integer arithmetic (hashes, random
    // numbers, colour packing), which GCC 4.4 compiled as plain wrapping instructions. Make it
    // defined, and keep UBSan's other checks.
    "-fwrapv",
    "-fno-sanitize=signed-integer-overflow,shift-base",
};

/// Emscripten's link settings for the game; web/index.html starts it with callMain once the
/// player's data is in the file system.
const emscripten_link_flags = [_][]const u8{
    "-sMIN_WEBGL_VERSION=2",
    "-sMAX_WEBGL_VERSION=2",
    // The renderer loads every GL entry point through SDL_GL_GetProcAddress.
    "-sGL_ENABLE_GET_PROC_ADDRESS=1",
    "-sALLOW_MEMORY_GROWTH=1",
    "-sSTACK_SIZE=1MB",
    "-sFORCE_FILESYSTEM=1",
    "-sINVOKE_RUN=0",
    "-sEXIT_RUNTIME=0",
    "-sEXPORTED_RUNTIME_METHODS=callMain,FS",
    // The page keeps the game data and the user data folder in IndexedDB.
    "-lidbfs.js",
};

/// The core of zlib: the game only uses deflate and inflate on memory (no gz* file API).
const zlib_sources = [_][]const u8{
    "adler32.c", "compress.c", "crc32.c",   "deflate.c", "infback.c", "inffast.c",
    "inflate.c", "inftrees.c", "trees.c",   "uncompr.c", "zutil.c",
};

/// PUC Lua 5.1.5's core and standard libraries (src/ without the lua and luac programs).
const lua_sources = [_][]const u8{
    "lapi.c",    "lcode.c",   "ldebug.c",   "ldo.c",      "ldump.c",   "lfunc.c",   "lgc.c",
    "llex.c",    "lmem.c",    "lobject.c",  "lopcodes.c", "lparser.c", "lstate.c",  "lstring.c",
    "ltable.c",  "ltm.c",     "lundump.c",  "lvm.c",      "lzio.c",    "lauxlib.c", "lbaselib.c",
    "ldblib.c",  "liolib.c",  "lmathlib.c", "loslib.c",   "ltablib.c", "lstrlib.c", "loadlib.c",
    "linit.c",
};

const lua_headers = [_][]const u8{ "src/lua.h", "src/luaconf.h", "src/lualib.h", "src/lauxlib.h", "etc/lua.hpp" };

/// Each dependency's licence files (a trailing '/' marks a directory).
const license_files = [_][2][]const u8{
    .{ "zlib", "LICENSE" },
    .{ "lua", "COPYRIGHT" },
    .{ "stb", "LICENSE" },
    .{ "miniaudio", "LICENSE" },
    .{ "sdl", "LICENSE.txt" },
    .{ "sdl", "LICENSES/" },
};

/// The third-party libraries every game module links.
const Libraries = struct {
    zlib: *std.Build.Step.Compile,
    lua: *std.Build.Step.Compile,
    stb_image: *std.Build.Step.Compile,
    miniaudio: *std.Build.Step.Compile,
    sdl: *std.Build.Step.Compile,

    fn all(libs: Libraries) [5]*std.Build.Step.Compile {
        return .{ libs.zlib, libs.lua, libs.stb_image, libs.miniaudio, libs.sdl };
    }
};

pub fn build(b: *std.Build) void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});
    const version = std.SemanticVersion.parse(@import("build.zig.zon").version) catch @panic("bad version in build.zig.zon");

    const libs: Libraries = .{
        .zlib = buildZlib(b, target, optimize),
        .lua = buildLua(b, target, optimize),
        .stb_image = buildStbImage(b, target, optimize),
        .miniaudio = buildMiniaudio(b, target, optimize),
        .sdl = buildSdl(b, target, optimize),
    };
    for (libs.all()) |lib| b.installArtifact(lib);

    const units = keptUnits(b);
    const port_sources = portSources(b);

    // The recovered code and the port's sources as a library: the default step, which builds while
    // the platform seams are still missing.
    const harvest_lib = b.addLibrary(.{
        .name = "harvest",
        .root_module = gameModule(b, target, optimize, libs, units, port_sources),
    });
    b.installArtifact(harvest_lib);

    if (target.result.os.tag == .emscripten) {
        b.step("harvest", "Build the game for the web").dependOn(webSite(b, target, optimize, harvest_lib, libs));
        return;
    }

    // The game. It links every kept object and port source, so it only links once every seam
    // exists; until then `zig build harvest` lists what is missing, like the census.
    const exe_module = gameModule(b, target, optimize, libs, units, port_sources);
    exe_module.addCSourceFile(.{ .file = b.path("src/main.cpp"), .flags = &cxx_flags });
    exe_module.strip = b.option(bool, "strip", "Leave the debug information out of the game executable (packages do)");
    const exe = b.addExecutable(.{ .name = "harvest", .root_module = exe_module });
    if (target.result.os.tag == .windows) {
        // Release builds are GUI programs (no console window); debug builds keep the console for
        // the log.
        exe.subsystem = if (optimize == .debug) .console else .windows;
        // The icon and version information.
        exe_module.addWin32ResourceFile(.{
            .file = b.path("packaging/windows/harvest.rc"),
            .flags = &.{
                b.fmt("/DVERSION_MAJOR={d}", .{version.major}),
                b.fmt("/DVERSION_MINOR={d}", .{version.minor}),
                b.fmt("/DVERSION_PATCH={d}", .{version.patch}),
            },
        });
    }
    const install_exe = b.addInstallArtifact(exe, .{});
    b.step("harvest", "Build the game executable").dependOn(&install_exe.step);

    const run = b.addRunArtifact(exe);
    run.step.dependOn(&install_exe.step);
    run.addPassthruArgs();
    b.step("run", "Run the game").dependOn(&run.step);

    // The licences of the libraries linked into the game, for packages (tools/package.sh).
    const licenses = b.step("licenses", "Install the linked libraries' licences into share/licenses");
    for (license_files) |license| {
        const path = std.mem.trimEnd(u8, license[1], "/");
        const source = b.dependency(license[0], .{}).path(path);
        const name = b.fmt("{s}/{s}", .{ license[0], path });
        licenses.dependOn(if (std.mem.endsWith(u8, license[1], "/"))
            &b.addInstallDirectory(.{ .source_dir = source, .install_dir = .{ .custom = "share/licenses" }, .install_subdir = name }).step
        else
            &b.addInstallFileWithDir(source, .{ .custom = "share/licenses" }, name).step);
    }

    // The census links the same objects against the original's plain loop (census/main.cpp)
    // instead of the SDL3 entry point, so the linker lists exactly what the recovered code and the
    // port's sources still need.
    const census_module = gameModule(b, target, optimize, libs, units, port_sources);
    census_module.addCSourceFile(.{ .file = b.path("census/main.cpp"), .flags = &cxx_flags });
    const census = b.addExecutable(.{ .name = "harvest-census", .root_module = census_module });
    b.step("census", "Link every kept unit and report unresolved symbols").dependOn(&b.addInstallArtifact(census, .{}).step);

    // Test programs: tests/<name>.cpp links against the library (only the objects it needs) and
    // runs with `zig build test-<name> -- args`; `zig build tests` builds them all without running.
    const tests_step = b.step("tests", "Build the test programs");
    for (testSources(b)) |source| {
        const name = source[0 .. source.len - ".cpp".len];
        const module = b.createModule(.{ .target = target, .optimize = optimize, .link_libcpp = true });
        addIncludePaths(b, module);
        addTargetMacros(module);
        module.addCSourceFile(.{ .file = b.path(b.pathJoin(&.{ "tests", source })), .flags = &cxx_flags });
        module.linkLibrary(harvest_lib);
        for (libs.all()) |lib| module.linkLibrary(lib);
        const test_exe = b.addExecutable(.{ .name = b.fmt("harvest-test-{s}", .{name}), .root_module = module });
        const install_test = b.addInstallArtifact(test_exe, .{});
        tests_step.dependOn(&install_test.step);
        const run_test = b.addRunArtifact(test_exe);
        run_test.step.dependOn(&install_test.step);
        run_test.addPassthruArgs();
        b.step(b.fmt("test-{s}", .{name}), b.fmt("Build and run tests/{s}", .{source})).dependOn(&run_test.step);
    }
}

/// The web build: harvest_lib and the libraries linked by em++ with src/main.cpp, next to the page.
fn webSite(
    b: *std.Build,
    target: std.Build.ResolvedTarget,
    optimize: std.builtin.OptimizeMode,
    harvest_lib: *std.Build.Step.Compile,
    libs: Libraries,
) *std.Build.Step {
    const main_module = b.createModule(.{ .target = target, .optimize = optimize });
    addIncludePaths(b, main_module);
    main_module.addCSourceFile(.{ .file = b.path("src/main.cpp"), .flags = &cxx_flags });
    main_module.linkLibrary(libs.sdl);
    const main_lib = b.addLibrary(.{ .name = "harvest-main", .root_module = main_module });

    const link = b.addSystemCommand(&.{"em++"});
    // The linker would not pull main out of an archive by itself.
    link.addArg("-Wl,--whole-archive");
    link.addArtifactArg(main_lib);
    link.addArg("-Wl,--no-whole-archive");
    link.addArtifactArg(harvest_lib);
    for (libs.all()) |lib| link.addArtifactArg(lib);
    link.addArgs(&emscripten_link_flags);
    // The main-thread half of the audio output (audio/WebAudioOutput.cpp declares its functions).
    link.addArg("--js-library");
    link.addFileArg(b.path("src/audio/WebAudioOutput.js"));
    link.addArgs(switch (optimize) {
        // The game code's UBSan checks call into the runtime Emscripten links with this.
        .Debug => &.{ "-g", "-fsanitize=undefined" },
        .ReleaseSafe, .ReleaseFast => &.{"-O3"},
        .ReleaseSmall => &.{"-Oz"},
    });
    link.addArg("-o");
    // harvest.wasm is written next to it.
    const js = link.addOutputFileArg("harvest.js");

    const step = b.step("web", "Install the page, harvest.js and harvest.wasm");
    step.dependOn(&b.addInstallDirectory(.{ .source_dir = js.dirname(), .install_dir = .prefix, .install_subdir = "" }).step);
    step.dependOn(&b.addInstallDirectory(.{ .source_dir = b.path("web"), .install_dir = .prefix, .install_subdir = "" }).step);
    // The audio output's worklet, which WebAudioOutput.js loads from beside the page.
    step.dependOn(&b.addInstallFile(b.path("src/audio/WebAudioWorklet.js"), "WebAudioWorklet.js").step);
    return step;
}

fn addIncludePaths(b: *std.Build, module: *std.Build.Module) void {
    module.addIncludePath(b.path("../src"));
    module.addIncludePath(b.path("../src/HarvestFull"));
    module.addIncludePath(b.path("src"));
    addMacosSdk(b, module);
    if (emscriptenSysroot(b, module.resolved_target.?)) |sysroot| {
        // Emscripten's libc++ in place of zig's, ahead of the compiler's own headers as libc++
        // needs (zig puts those first among the system paths, so this one is not a system path).
        module.addIncludePath(.{ .cwd_relative = b.pathJoin(&.{ sysroot, "include/c++/v1" }) });
        addEmscriptenSysroot(b, module);
    }
}

/// Target-dependent macros for the game's C++ (the recovered code, the port and the tests).
fn addTargetMacros(module: *std.Build.Module) void {
    // mingw-w64's own printf family instead of the CRT's: the recovered code formats wide strings
    // with glibc's rules (in a wide format, %s is a narrow string and %S or %ls a wide one), and
    // the CRT's legacy wide specifiers swap %s and %S, so every localized "%s" printed only the
    // first character of its argument.
    if (module.resolved_target.?.result.os.tag == .windows) module.addCMacro("__USE_MINGW_ANSI_STDIO", "1");
    // On the web the mixer plays through its own AudioWorklet output (audio/WebAudioOutput.cpp), so
    // miniaudio is only the decoder there; the header must agree with how miniaudio.c is built.
    if (module.resolved_target.?.result.os.tag == .emscripten) module.addCMacro("MA_NO_DEVICE_IO", "1");
}

/// The .cpp files directly in tests/.
fn testSources(b: *std.Build) []const []const u8 {
    const io = b.graph.io;
    const root = b.root.join(b.allocator, "tests") catch @panic("OOM");
    var dir = root.root_dir.handle.openDir(io, root.sub_path, .{ .iterate = true }) catch return &.{};
    defer dir.close(io);
    b.dependOnDirectoryContents(b.path("tests"));

    var sources: std.ArrayList([]const u8) = .empty;
    var it = dir.iterate();
    while (it.next(io) catch |err| std.debug.panic("cannot list tests: {t}", .{err})) |entry| {
        if (entry.kind == .file and std.mem.endsWith(u8, entry.name, ".cpp"))
            sources.append(b.allocator, b.dupe(entry.name)) catch @panic("OOM");
    }
    std.mem.sort([]const u8, sources.items, {}, lessThan);
    return sources.items;
}

fn gameModule(
    b: *std.Build,
    target: std.Build.ResolvedTarget,
    optimize: std.builtin.OptimizeMode,
    libs: Libraries,
    units: []const []const u8,
    port_sources: []const []const u8,
) *std.Build.Module {
    const module = b.createModule(.{
        .target = target,
        .optimize = optimize,
        // Emscripten brings its own libc++ (see addIncludePaths), linked by em++.
        .link_libcpp = target.result.os.tag != .emscripten,
    });
    addIncludePaths(b, module);
    addTargetMacros(module);
    module.addCSourceFiles(.{ .root = b.path("../src"), .files = units, .flags = &cxx_flags });
    module.addCSourceFiles(.{ .root = b.path("src"), .files = port_sources, .flags = &cxx_flags });
    for (libs.all()) |lib| module.linkLibrary(lib);
    return module;
}

/// SDL3 as a static library, given the macOS SDK where zig does not find it itself.
fn buildSdl(b: *std.Build, target: std.Build.ResolvedTarget, optimize: std.builtin.OptimizeMode) *std.Build.Step.Compile {
    if (emscriptenSysroot(b, target)) |sysroot| return b.dependency("sdl", .{
        .target = target,
        .optimize = optimize,
        .preferred_linkage = .static,
        .system_include_path = std.Build.LazyPath{ .cwd_relative = b.pathJoin(&.{ sysroot, "include" }) },
    }).artifact("SDL3");
    const dep = if (macosSdk(b, target)) |sdk| b.dependency("sdl", .{
        .target = target,
        .optimize = optimize,
        .preferred_linkage = .static,
        .system_include_path = std.Build.LazyPath{ .cwd_relative = b.pathJoin(&.{ sdk, "usr/include" }) },
        .system_framework_path = std.Build.LazyPath{ .cwd_relative = b.pathJoin(&.{ sdk, "System/Library/Frameworks" }) },
        .library_path = std.Build.LazyPath{ .cwd_relative = b.pathJoin(&.{ sdk, "usr/lib" }) },
    }) else b.dependency("sdl", .{
        .target = target,
        .optimize = optimize,
        .preferred_linkage = .static,
    });
    return dep.artifact("SDL3");
}

/// The macOS SDK's path, from xcrun, for a macOS target zig does not treat as native (any
/// -Dtarget, such as x86_64-macos on an arm64 Mac): zig only finds the SDK by itself for the host,
/// and the system headers and frameworks come from it. Null for other targets.
fn macosSdk(b: *std.Build, target: std.Build.ResolvedTarget) ?[]const u8 {
    if (target.result.os.tag != .macos or target.query.isNative()) return null;
    const cache = struct {
        var path: ?[]const u8 = null;
    };
    if (cache.path == null)
        cache.path = std.mem.trim(u8, b.run(&.{ "xcrun", "--sdk", "macosx", "--show-sdk-path" }), " \t\r\n");
    return cache.path;
}

fn addMacosSdk(b: *std.Build, module: *std.Build.Module) void {
    const sdk = macosSdk(b, module.resolved_target.?) orelse return;
    module.addSystemIncludePath(.{ .cwd_relative = b.pathJoin(&.{ sdk, "usr/include" }) });
    module.addSystemFrameworkPath(.{ .cwd_relative = b.pathJoin(&.{ sdk, "System/Library/Frameworks" }) });
    module.addLibraryPath(.{ .cwd_relative = b.pathJoin(&.{ sdk, "usr/lib" }) });
}

/// Emscripten's sysroot, from em-config, for the web target: zig has no libc for it. Null for
/// other targets.
fn emscriptenSysroot(b: *std.Build, target: std.Build.ResolvedTarget) ?[]const u8 {
    if (target.result.os.tag != .emscripten) return null;
    const cache = struct {
        var path: ?[]const u8 = null;
    };
    if (cache.path == null)
        cache.path = b.pathJoin(&.{ std.mem.trim(u8, b.run(&.{ "em-config", "CACHE" }), " \t\r\n"), "sysroot" });
    return cache.path;
}

/// Emscripten's C headers and the compatibility headers its libc++ includes.
fn addEmscriptenSysroot(b: *std.Build, module: *std.Build.Module) void {
    const sysroot = emscriptenSysroot(b, module.resolved_target.?) orelse return;
    module.addSystemIncludePath(.{ .cwd_relative = b.pathJoin(&.{ sysroot, "include/compat" }) });
    module.addSystemIncludePath(.{ .cwd_relative = b.pathJoin(&.{ sysroot, "include" }) });
}

fn cModule(b: *std.Build, target: std.Build.ResolvedTarget, optimize: std.builtin.OptimizeMode) *std.Build.Module {
    const module = b.createModule(.{
        .target = target,
        .optimize = optimize,
        .link_libc = true,
        // Third-party C keeps its own (well-defined in practice) undefined behaviour; UBSan stays
        // on for the game code.
        .sanitize_c = .off,
    });
    addMacosSdk(b, module);
    addEmscriptenSysroot(b, module);
    return module;
}

fn buildZlib(b: *std.Build, target: std.Build.ResolvedTarget, optimize: std.builtin.OptimizeMode) *std.Build.Step.Compile {
    const dep = b.dependency("zlib", .{});
    const module = cModule(b, target, optimize);
    module.addCSourceFiles(.{ .root = dep.path(""), .files = &zlib_sources, .flags = &.{"-std=c11"} });
    const lib = b.addLibrary(.{ .name = "z", .root_module = module });
    lib.installHeader(dep.path("zlib.h"), "zlib.h");
    lib.installHeader(dep.path("zconf.h"), "zconf.h");
    return lib;
}

fn buildLua(b: *std.Build, target: std.Build.ResolvedTarget, optimize: std.builtin.OptimizeMode) *std.Build.Step.Compile {
    const dep = b.dependency("lua", .{});
    const module = cModule(b, target, optimize);
    // POSIX extras (mkstemp, popen, isatty) everywhere but Windows; no dynamic C modules.
    if (target.result.os.tag != .windows) module.addCMacro("LUA_USE_POSIX", "1");
    module.addCSourceFiles(.{
        .root = dep.path("src"),
        .files = &lua_sources,
        // Lua's errors are setjmp/longjmp, which on the web need Emscripten's lowering (em++ links its
        // runtime).
        .flags = if (target.result.os.tag == .emscripten)
            &.{ "-std=gnu99", "-mllvm", "-enable-emscripten-sjlj" }
        else
            &.{"-std=gnu99"},
    });
    const lib = b.addLibrary(.{ .name = "lua", .root_module = module });
    for (lua_headers) |header| lib.installHeader(dep.path(header), std.fs.path.basename(header));
    return lib;
}

fn buildStbImage(b: *std.Build, target: std.Build.ResolvedTarget, optimize: std.builtin.OptimizeMode) *std.Build.Step.Compile {
    const dep = b.dependency("stb", .{});
    const module = cModule(b, target, optimize);
    module.addIncludePath(dep.path(""));
    module.addCSourceFile(.{ .file = b.path("src/thirdparty/stb_image.c"), .flags = &.{"-std=c99"} });
    module.addCSourceFile(.{ .file = b.path("src/thirdparty/stb_image_write.c"), .flags = &.{"-std=c99"} });
    const lib = b.addLibrary(.{ .name = "stb_image", .root_module = module });
    lib.installHeader(dep.path("stb_image.h"), "stb_image.h");
    lib.installHeader(dep.path("stb_image_write.h"), "stb_image_write.h");
    return lib;
}

fn buildMiniaudio(b: *std.Build, target: std.Build.ResolvedTarget, optimize: std.builtin.OptimizeMode) *std.Build.Step.Compile {
    const dep = b.dependency("miniaudio", .{});
    const module = cModule(b, target, optimize);
    module.addIncludePath(dep.path(""));
    // Only the decoders on the web (see addTargetMacros).
    if (target.result.os.tag == .emscripten) module.addCMacro("MA_NO_DEVICE_IO", "1");
    module.addCSourceFile(.{ .file = b.path("src/thirdparty/miniaudio.c"), .flags = &.{"-std=gnu99"} });
    // miniaudio loads the platform audio libraries at run time; on Linux that needs libdl.
    if (target.result.os.tag == .linux) {
        module.linkSystemLibrary("dl", .{});
        module.linkSystemLibrary("pthread", .{});
        module.linkSystemLibrary("m", .{});
    }
    const lib = b.addLibrary(.{ .name = "miniaudio", .root_module = module });
    lib.installHeader(dep.path("miniaudio.h"), "miniaudio.h");
    return lib;
}

/// The units listed in units.toml (`["path.cpp"]` table headers) minus the replaced ones.
fn keptUnits(b: *std.Build) []const []const u8 {
    // Reconfigure when the unit list changes instead of poisoning the configuration cache.
    b.dependOnFileContents(b.path(units_toml));
    const file = b.root.join(b.allocator, units_toml) catch @panic("OOM");
    const text = file.root_dir.handle.readFileAlloc(b.graph.io, file.sub_path, b.allocator, .limited(1 << 20)) catch |err|
        std.debug.panic("cannot read {s}: {t}", .{ units_toml, err });

    var units: std.ArrayList([]const u8) = .empty;
    var lines = std.mem.tokenizeScalar(u8, text, '\n');
    while (lines.next()) |raw| {
        const line = std.mem.trim(u8, raw, " \t\r");
        if (!std.mem.startsWith(u8, line, "[\"") or !std.mem.endsWith(u8, line, "\"]")) continue;
        const unit = line[2 .. line.len - 2];
        if (isReplaced(unit)) continue;
        units.append(b.allocator, unit) catch @panic("OOM");
    }
    return units.items;
}

fn isReplaced(unit: []const u8) bool {
    for (replaced) |prefix| {
        if (std.mem.startsWith(u8, unit, prefix)) return true;
    }
    return false;
}

/// Every .cpp file under src/ (paths relative to it), except the entry point.
fn portSources(b: *std.Build) []const []const u8 {
    const io = b.graph.io;
    const root = b.root.join(b.allocator, "src") catch @panic("OOM");
    var dir = root.root_dir.handle.openDir(io, root.sub_path, .{ .iterate = true }) catch |err|
        std.debug.panic("cannot open src: {t}", .{err});
    defer dir.close(io);
    b.dependOnDirectoryContents(b.path("src"));

    var sources: std.ArrayList([]const u8) = .empty;
    var walker = dir.walk(b.allocator) catch @panic("OOM");
    defer walker.deinit();
    while (walker.next(io) catch |err| std.debug.panic("cannot list src: {t}", .{err})) |entry| {
        const path = b.dupe(entry.path);
        switch (entry.kind) {
            // Reconfigure when files are added to or removed from any directory.
            .directory => b.dependOnDirectoryContents(b.path(b.pathJoin(&.{ "src", path }))),
            .file => if (std.mem.endsWith(u8, path, ".cpp") and !std.mem.eql(u8, path, "main.cpp"))
                sources.append(b.allocator, path) catch @panic("OOM"),
            else => {},
        }
    }
    // Directory order is unspecified; sort so the configuration is stable.
    std.mem.sort([]const u8, sources.items, {}, lessThan);
    return sources.items;
}

fn lessThan(_: void, a: []const u8, b: []const u8) bool {
    return std.mem.lessThan(u8, a, b);
}
